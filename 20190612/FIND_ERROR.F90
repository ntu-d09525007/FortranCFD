SUBROUTINE FIND_ERROR(R,A,B,BTN)
USE PRECISION
USE PROBLEM_DEF
IMPLICIT NONE
INTEGER :: I,J,K,BTN
REAL(DP),DIMENSION(-2:NODE_X+3,-2:NODE_Y+3,-2:NODE_Z+3) :: A,B
REAL(DP) :: R

! 1 -- L1 NORM
! 2 -- L2 NORM
! 3 -- Linf NORM

R = 0.0_DP

IF( BTN==1 )THEN

 !$OMP PARALLEL DO REDUCTION(+:R)
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X
	 R = R + ABS(A(I,J,K)-B(I,J,K))
 END DO
 END DO
 END DO
 !$OMP END PARALLEL DO
 
 R = R / REAL(NODE_Z*NODE_Y*NODE_X,KIND=DP)
 
ELSE IF( BTN==2 )THEN

 !$OMP PARALLEL DO REDUCTION(+:R)
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X
	 R = R + (A(I,J,K)-B(I,J,K))**2
 END DO
 END DO
 END DO
 !$OMP END PARALLEL DO
 
 R = DSQRT( R / REAL(NODE_Z*NODE_Y*NODE_X,KIND=DP) )
 
ELSE 
	
 !$OMP PARALLEL DO REDUCTION(MAX:R)
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X
	 R = MAX(R,ABS(A(I,J,K)-B(I,J,K)))
 END DO
 END DO
 END DO
 !$OMP END PARALLEL DO
 
END IF

END SUBROUTINE