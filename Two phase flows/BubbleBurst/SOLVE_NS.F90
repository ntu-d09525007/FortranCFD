SUBROUTINE SOLVE_NS()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
USE LS_DATA
IMPLICIT NONE
INTEGER :: INITER,I,J,K

  CALL AMURHO()
  CALL FIND_CURVATURE()

  INITER = 0

  DO

    INITER = INITER + 1

    !$OMP PARALLEL DO
    DO K = -2, NODE_Z+3
    DO J = -2, NODE_Y+3
    DO I = -2, NODE_X+3
      U_TMP(I,J,K) = U(I,J,K)
      V_TMP(I,J,K) = V(I,J,K)
      W_TMP(I,J,K) = W(I,J,K)
    END DO
    END DO
    END DO
    !$OMP END PARALLEL DO

   
    CALL SECOND_ORDER_UPWIND
    !CALL QUICK  ! need to be checked 2019.09.27

    !CALL PRESSURE_PREDICTOR
    
    IF( REC_MASS==0 )RETURN

    CALL GS_PROJ_SOLVER
    !CALL FPC_PROJ_SOLVER

    CALL CHECK_CONVERGENCE(INITER)

    CALL VELOCITY_BC()

    IF( INITER>2 )EXIT

  END DO

  !CALL PRESSURE_CORRECTION

END SUBROUTINE

SUBROUTINE CHECK_CONVERGENCE(INITER)
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
IMPLICIT NONE
INTEGER :: INITER,I,J,K
REAL(DP) :: RES

  CALL FIND_ERROR(L2_U,U,U_TMP,2)
  CALL FIND_ERROR(L2_V,V,V_TMP,2)
  CALL FIND_ERROR(L2_W,W,W_TMP,2)
 
  CALL FIND_ERROR(LINF_U,U,U_TMP,3)
  CALL FIND_ERROR(LINF_V,V,V_TMP,3)
  CALL FIND_ERROR(LINF_W,W,W_TMP,3)
  
  !IF( max(LINF_U,LINF_V,LINF_W) < TOL_M )INITER = INITER + 5

 DIV = 0.0_DP
 
 !$omp parallel do REDUCTION(MAX:DIV),PRIVATE(RES) 
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X

    RES=(U(I,J,K)-U(I-1,J,K))/DX &
       +(V(I,J,K)-V(I,J-1,K))/DY &
       +(W(I,J,K)-W(I,J,K-1))/DZ
       
    DIV = MAX(DIV,ABS(RES))
       
  ENDDO
  ENDDO
  ENDDO
  !$omp end parallel do    

END SUBROUTINE

SUBROUTINE VELOCITY_BC()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
USE IBM_DATA
implicit none
integer :: i,j,k
real(dp) :: s

  CALL BCU(U)
  CALL BCV(V)
  CALL BCW(W)
      
END SUBROUTINE

SUBROUTINE SOLVE_MOMENTUM()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
IMPLICIT NONE
INTEGER :: I,J,K

!$omp parallel do
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X-1
   U1(I,J,K)= -U1(I,J,K)-2.0_DP*(P(I+1,J,K)-P(I,J,K))/(DX*(RHO(I,J,K)+RHO(I+1,J,K)))
   U1(I,J,K)=  U_OLD(I,J,K)+U1(I,J,K)*DT
   U(i,j,k) =  U1(i,j,k)
 END DO
 END DO
 END DO
!$omp end parallel do  

!$omp parallel do
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y-1 
 DO I = 1, NODE_X 
   V1(I,J,K)= -V1(I,J,K)-2.0_DP*(P(I,J+1,K)-P(I,J,K))/(DY*(RHO(I,J,K)+RHO(I,J+1,K)))
   V1(I,J,K)=  V_OLD(I,J,K)+V1(I,J,K)*DT
   V(i,j,k) =  V1(i,j,k)
 END DO
 END DO
 END DO
!$omp end parallel do  

!$omp parallel do
 DO K = 1, NODE_Z-1
 DO J = 1, NODE_Y
 DO I = 1, NODE_X 
   W1(I,J,K)= -W1(I,J,K)-2.0_DP*(P(I,J,K+1)-P(I,J,K))/(DZ*(RHO(I,J,K)+RHO(I,J,K+1)))
   W1(I,J,K)=  W_OLD(I,J,K)+W1(I,J,K)*DT
   W(i,j,k) =  W1(i,j,k)
 END DO
 END DO
 END DO
 !$omp end parallel do  

END SUBROUTINE

SUBROUTINE Adams_Bashforth()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
IMPLICIT NONE
INTEGER :: I,J,K

 !$OMP PARALLEL DO
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X
  U1(I,J,K) = 1.5_DP* US(I,J,K)  - 0.5_DP*US_OLD(I,J,K) 
  V1(I,J,K) = 1.5_DP* VS(I,J,K)  - 0.5_DP*VS_OLD(I,J,K) 
  W1(I,J,K) = 1.5_DP* WS(I,J,K)  - 0.5_DP*WS_OLD(I,J,K)
 END DO
 END DO
 END DO
 !$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE PRESSURE_PREDICTOR()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
IMPLICIT NONE
INTEGER :: I,J,K
REAL(DP) :: RX, RY, RZ

  !$OMP PARALLEL DO PRIVATE(RX,RY,RZ)
  DO K = 0, NODE_Z
  DO J = 0, NODE_Y
  DO I = 0, NODE_X
  
	RX = 0.5_DP*( RHO_OLD(I+1,J,K)+RHO_OLD(I,J,K) )*DX
	RY = 0.5_DP*( RHO_OLD(I,J+1,K)+RHO_OLD(I,J,K) )*DY
	RZ = 0.5_DP*( RHO_OLD(I,J,K+1)+RHO_OLD(I,J,K) )*DZ
	
	US(I,J,K) = US(I,J,K) + 0.5_DP*(P_OLD(I+1,J,K)-P_OLD(I,J,K))/RX
	VS(I,J,K) = VS(I,J,K) + 0.5_DP*(P_OLD(I,J+1,K)-P_OLD(I,J,K))/RY
	WS(I,J,K) = WS(I,J,K) + 0.5_DP*(P_OLD(I,J,K+1)-P_OLD(I,J,K))/RZ
  
  ENDDO
  ENDDO
  ENDDO
  !$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE PRESSURE_CORRECTION()
USE PRECISION
USE PROBLEM_DEF
USE FLUID_PROPERTIES
IMPLICIT NONE
INTEGER :: I,J,K

 !$OMP PARALLEL DO
 DO K = 1, NODE_Z
 DO J = 1, NODE_Y
 DO I = 1, NODE_X
   P(I,J,K) = P(I,J,K)+P_OLD(I,J,K)
 END DO
 END DO
 END DO
 !$OMP END PARALLEL DO
 

END SUBROUTINE
 
